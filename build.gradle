plugins {
    id 'java'
    id 'groovy'
    id 'eclipse'
    id 'maven-publish'
    id 'signing'
    id 'application'
}

compileJava.options.encoding = 'UTF-8'


group = 'ua.net.nlp'
ext.artifactId = 'nlp_uk'

repositories {
    mavenCentral()
    mavenLocal()
}


dependencies {
    implementation group: 'org.apache.groovy', name: 'groovy', version: groovyVersion
    implementation group: 'org.apache.groovy', name: 'groovy-json', version: groovyVersion
    implementation group: 'org.apache.groovy', name: 'groovy-xml', version: groovyVersion

    implementation group: 'info.picocli', name: 'picocli', version: '4.7.0'
    implementation group: 'ch.qos.logback', name:'logback-classic', version:'1.4.5'

    implementation("org.languagetool:language-uk:$ltUkVersion") {
        exclude module: 'hunspell'
        exclude module: 'language-detector'
        exclude module: 'lucene-core'
        exclude group: 'org.apache.lucene'
        exclude group: 'io.grpc'
    }
    implementation ("org.languagetool:language-ru:$ltVersion") {
        exclude module: 'hunspell'
        exclude module: 'language-detector'
        exclude module: 'lucene-core'
        exclude group: 'org.apache.lucene'
        exclude group: 'io.grpc'
    }

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.+'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.9.+'
}

sourceSets {
    main {
        resources {
            srcDir "src/main/groovy"
//            include "**/*.groovy"
//            includes = ["**/tools/*.md", "*.groovy", "**/*.txt"]
        }
    }
}

processResources {
    doLast() {
        copy {
            from("src/main/groovy/") {
              include "org/nlp_uk/tools/TagText.groovy"
            }
//            into "${project.buildDir}/resources/main"
            into "${project.buildDir}/classes/java/main"
            includeEmptyDirs = false
        }
    }
}

compileGroovy {
    exclude '**/demo/**/*.groovy'
//    exclude '**/other/**/*.groovy'

    groovyOptions.forkOptions.jvmArgs = [ '-Dgroovy.grape.enable=false' ]
}


eclipse {
  classpath {
    defaultOutputDir = file('build')
    file {
        whenMerged { classpath ->
            classpath.entries.findAll { entry -> entry.kind == 'lib' && entry.path =~ /languagetool-core|language-(uk|ru)/ }*.exported = true
        }
    }
  }
}

task downloadResources(type: JavaExec) {
    classpath = sourceSets.main.runtimeClasspath
    mainClass = "ua.net.nlp.tools.tag.TagTextCore"
    args "--download"
}

test {
    useJUnitPlatform()
    jvmArgs '-Dgroovy.grape.enable=false'
}


task tagText(type: JavaExec) {
    classpath = sourceSets.main.runtimeClasspath
    mainClass = "ua.net.nlp.tools.tag.TagTextCore"

//    jvmArgs '-Dgroovy.grape.enable=false'

    String argParam = project.hasProperty('args') ? project['args'] : ''
    args argParam.split()
}


task testScripts(type: Exec) {
    commandLine 'echo "я ти" | groovy src/main/groovy/ua/net/nlp/tools/TokenizeText.groovy -i -'
}


// fatJar does not work due to LT having same file in different language modules
task fatJar(type: Jar) {
    manifest {
        attributes 'Implementation-Title': 'NLP Tools for Ukrainian',
            'Implementation-Version': archiveVersion
    }
    archiveBaseName = project.name + '-all'
//    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    from { configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}

task copyRuntimeLibs(type: Copy) {
    into "${buildDir}/lib"
    from configurations.runtimeClasspath

    doFirst {
        delete files("${buildDir}/lib")
    }
}

application {
    mainClass = "ua.net.nlp.tools.tag.TagTextCore"
    
//    def args = ['-Dgroovy.grape.enable=false', '-i', '../media_main/td.txt', '-o', 'td.xml']
//    applicationDefaultJvmArgs = ['-Dgroovy.grape.enable=false']
//    applicationDefaultJvmArgs = ['-Dgroovy.grape.enable=false', '-i', '../../media_main/td.txt', '-o', 'td.xml', "-XX:+FlightRecorder", "-XX:StartFlightRecording=disk=true,dumponexit=true,filename=recording.jfr"]
    applicationDefaultJvmArgs = ['-Dgroovy.grape.enable=false', "-XX:+FlightRecorder", "-XX:StartFlightRecording=disk=true,dumponexit=true,filename=recording.jfr"]
    //applicationDefaultJvmArgs = args
}

//compileJava.enabled = false

jar {

    manifest {
        attributes 'Implementation-Title': 'NLP tools for Ukrainian language',
                   'Implementation-Version': project.version
    }

    setArchiveFileName "${artifactId}-${project.version}.jar"

    includeEmptyDirs false
    exclude("ua/net/nlp/tools/TagText.class")
    exclude("ua/net/nlp/tools/TokenizeText.class")
    exclude("ua/net/nlp/tools/CleanText.class")
    exclude("ua/net/nlp/tools/CheckText.class")
    exclude("ua/net/nlp/tools/EvaluateText.class")
    exclude("ua/net/nlp/tools/ExtractText.class")
    exclude("ua/net/nlp/tools/Inflect.class")
    exclude("ua/net/nlp/tools/StressText.class")
}


task sourceJar(type: Jar) {
    archiveClassifier = "sources"
    from sourceSets.main.allGroovy
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier = "javadoc"
    from javadoc.destinationDir
}

if( project.hasProperty("ossrhUsername") ) {
    signing {
        sign configurations.archives
        sign publishing.publications
    }
}

publishing {
    publications {
        maven(MavenPublication) {
//            groupId = group
            artifactId = artifactId
//            version = version
            from components.java

            artifact(sourceJar) {
                classifier = 'sources'
            }
            artifact(javadocJar) {
                classifier = 'javadoc'
            }
            
            pom {
                name = 'NLP Tools for Ukrainian'
                description = 'NLP tools for Ukrainian language'

                url = "https://github.com/brown-uk/nlp_uk"
                
                licenses {
                  license {
                    name = 'GNU General Public License v3'
                    url = 'https://www.gnu.org/licenses/gpl-3.0.txt'
                  }
                }
                
                developers {
                  developer {
                    id = 'arysin'
                    name = 'Andriy Rysin'
                    email = 'arysin@gmail.com'
                  }
                }
                
                scm {
                  url = "https://github.com/brown-uk/nlp_uk.git"
                }
            }
        }
    }

    repositories {
        mavenLocal()

        if( ! project.hasProperty("local") ) {
            maven {
                if( project.hasProperty("ossrhUsername") ) {
                    credentials {
                        username = getProperty('ossrhUsername')
                        password = getProperty('ossrhPassword')
                    }
                }
                else {
                    logger.lifecycle "Not ossrh username/password found, remote pushing is disabled"
                }

                url =  version.contains("SNAPSHOT")
                        ? "https://oss.sonatype.org/content/repositories/snapshots/"
                        : "https://oss.sonatype.org/service/local/staging/deploy/maven2"
            }
        }
    }
}


// windows have non-unicode encoding set by default
String osName = System.getProperty("os.name").toLowerCase();
if ( osName.contains("windows")) {
    if( ! "UTF-8".equals(System.getProperty("file.encoding")) ) {
        println "On Windows to get unicode handled correctly you need to set environment variable before running expand:"
        println "\tbash:"
        println "\t\texport JAVA_TOOL_OPTIONS=-Dfile.encoding=UTF-8"
        println "\tcmd:"
        println "\t\t(change Font to 'Lucida Console' in cmd window properties)"
        println "\t\tchcp 65001"
        println "\t\tset JAVA_TOOL_OPTIONS=-Dfile.encoding=UTF-8"
        println "\n\tNOTE: bash shell (e.g. git bash) is recommended: only in bash the output will handle all cyrillics and expandInteractive only supported in bash"

        // poor man's safety net (may work but still will not show all cyrillic characters)
        tasks.each { task ->
            if( task instanceof JavaExec ) {
                task.jvmArgs '-Dfile.encoding=UTF-8'
            }
        }
    }
}
