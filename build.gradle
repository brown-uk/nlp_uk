apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: 'eclipse'

compileJava.options.encoding = 'UTF-8'

def ltLibDir=ltDir + '/languagetool-standalone/target/LanguageTool-*-SNAPSHOT/LanguageTool-*-SNAPSHOT/libs'


buildscript {
  repositories {
    mavenCentral()
  }
  dependencies {
    classpath group: 'org.apache.maven', name: 'maven-embedder', version: '3.3.9'
    classpath group: 'org.codehaus.plexus', name: 'plexus-interpolation', version: '1.22'
    classpath group: 'org.slf4j', name: 'slf4j-log4j12', version: '1.7.21'
  }
}

repositories {
    mavenCentral()
}

dependencies {
    compile group: 'org.codehaus.groovy', name: 'groovy-all', version: '2.4.7'

//    compile 'org.codehaus.gpars:gpars:1.2.1'
//    compile group: 'org.apache.logging.log4j', name: 'log4j-api', version: '2.5'
//    compile group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.5'
    compile 'commons-cli:commons-cli:1.3'

    compile files(ltDir + '/languagetool-core/target/classes')
    compile files(ltDir + '/languagetool-language-modules/uk/target/classes')
    compile fileTree(dir: ltLibDir, include: ['*.jar'], exclude: ['languagetool*.jar'])

//    testCompile 'junit:junit:4.12'
}

compileGroovy {
    exclude '**/demo/**/*.groovy'
}

sourceSets {
    main {
        groovy {
            srcDirs = ['src']
        }
    }
}

eclipse {
  classpath {
    defaultOutputDir = file('build')
  }
}

compileGroovy {
  groovyOptions.forkOptions.jvmArgs = [ '-Dgroovy.grape.enable=false' ]
}


import org.apache.maven.cli.*

task installLtCore() {
    doLast {
        MavenCli cli = new MavenCli();

        def projectRoot="$ltDir/languagetool-core"
        System.setProperty("maven.multiModuleProjectDirectory", projectRoot)

        cli.doMain(["clean", "install"].toArray(new String[0]), projectRoot, System.out, System.out);
    }
}

task buildLtUk() {
    doLast {
        MavenCli cli = new MavenCli();

        def testToRun="org.languagetool.tagging.disambiguation.rules.uk.UkrainianDisambiguationRuleTest"
        def projectRoot="$ltDir/languagetool-language-modules/uk"
        System.setProperty("maven.multiModuleProjectDirectory", projectRoot)

        if( ! project.hasProperty('allTests') ) {
            System.setProperty("test", "$testToRun")
        }

        cli.doMain(["compile", "test"].toArray(new String[0]), projectRoot, System.out, System.out);
    }
}


task devTagText (type: JavaExec, dependsOn: classes) {
    systemProperties << ['groovy.grape.enable': 'false']
    classpath = sourceSets.main.runtimeClasspath

    main = "org.nlp_uk.tools.TagText"

    def outputFile=new File(inputFile).name.replaceFirst(/\.txt$/, '') + ".tagged.txt"

    args "-i", inputFile, "-o", outputFile

    if( project.hasProperty('disabledRules') ) {
        logger.lifecycle("Disabled rules: " + disabledRules.join(', '))
        args "-d", disabledRules
    }

    doFirst {
        logger.lifecycle("Tagging $inputFile...")
    }
}

def get_diff_cmd(outputDir, file) {
    return "diff -u ${file}.old $file > ${file}.diff || echo 'File $file differs!'"
}


task devTagTextDiff(type: Exec) {
    def fileToDiff = new File(inputFile).name.replaceFirst(/\.txt$/, '') + '.tagged.txt'
    def cmd = get_diff_cmd('', fileToDiff)

    commandLine "sh", "-c", "${cmd}"

    doFirst {
        logger.lifecycle("Diffing $fileToDiff...")
    }
}



// windows have non-unicode encoding set by default
String osName = System.getProperty("os.name").toLowerCase();
if ( osName.contains("windows")) {
    if( ! "UTF-8".equals(System.getProperty("file.encoding")) ) {
        println "On Windows to get unicode handled correctly you need to set environment variable before running expand:"
        println "\tbash:"
        println "\t\texport JAVA_TOOL_OPTIONS=-Dfile.encoding=UTF-8"
        println "\tcmd:"
        println "\t\t(change Font to 'Lucida Console' in cmd window properties)"
        println "\t\tchcp 65001"
        println "\t\tset JAVA_TOOL_OPTIONS=-Dfile.encoding=UTF-8"
        println "\n\tNOTE: bash shell (e.g. git bash) is recommended: only in bash the output will handle all cyrillics and expandInteractive only supported in bash"

        // poor man's safety net (may work but still will not show all cyrillic characters)
        tasks.each { task ->
            if( task instanceof JavaExec ) {
                task.jvmArgs '-Dfile.encoding=UTF-8'
            }
        }
    }
}
